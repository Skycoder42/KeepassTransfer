VERSION = 3.0.0

CONFIG += warning_clean exceptions c++14
DEFINES += QT_DEPRECATED_WARNINGS QT_ASCII_CAST_WARNINGS QT_USE_QSTRINGBUILDER

PROJECT_NAME = KeePass-Transfer
PROJECT_TARGET = kptransfer
QMAKE_TARGET_PRODUCT = "$$PROJECT_NAME"
QMAKE_TARGET_COMPANY = "Skycoder42"
QMAKE_TARGET_COPYRIGHT = "Felix Barz"
QMAKE_TARGET_BUNDLE_PREFIX = de.skycoder42

DEFINES += "PROJECT_NAME=\\\"$$PROJECT_NAME\\\""
DEFINES += "PROJECT_TARGET=\\\"$$PROJECT_TARGET\\\""
DEFINES += "VERSION=\\\"$$VERSION\\\""
DEFINES += "COMPANY=\"\\\"$$QMAKE_TARGET_COMPANY\\\"\""
DEFINES += "BUNDLE=\"\\\"$$QMAKE_TARGET_BUNDLE_PREFIX\\\"\""
DEFINES += APPID=\"\\\"$${QMAKE_TARGET_BUNDLE_PREFIX}.$${PROJECT_TARGET}\\\"\"

ICON_SIZES=16 22 24 32 36 48 64 72 96 128 144 192 256 384 512

wasm {
	CONFIG += no_clients
	QMAKE_LFLAGS_DEBUG += --emrun
	QMAKE_CXXFLAGS += -s DISABLE_EXCEPTION_CATCHING=0
	QMAKE_LFLAGS += -s DISABLE_EXCEPTION_CATCHING=0
	DEFINES += QT_OS_WASM
}

android {
	PREFIX = /
	CONFIG += no_widgets no_webapp
}

!wasm:CONFIG(release, debug|release): CONFIG += no_webapp

SRC_ROOT_DIR=$$PWD

unix:!wasm:!cross_compile:packagesExist(libcrypto++): CONFIG += system_cryptopp
